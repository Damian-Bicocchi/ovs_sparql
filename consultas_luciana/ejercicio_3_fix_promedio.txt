# ¿Cuál es el precio en dólares por metro
# cuadrado de los terrenos en La Plata?

prefix : <http://www.semanticweb.org/luciana/ontologies/2024/8/inmontology#>
prefix brick: <https://brickschema.org/schema/Brick#>
prefix dc: <http://purl.org/dc/elements/1.1/>
prefix dcterms: <http://purl.org/dc/terms/>
prefix foaf: <http://xmlns.com/foaf/0.1/>
prefix gr: <http://purl.org/goodrelations/v1#>
prefix ns: <http://www.w3.org/2003/06/sw-vocab-status/ns#>
prefix owl: <http://www.w3.org/2002/07/owl#>
prefix pronto: <https://raw.githubusercontent.com/fdioguardi/pronto/main/ontology/pronto.owl#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix rec: <https://w3id.org/rec#>
prefix sioc: <http://rdfs.org/sioc/ns#>
prefix skos: <http://www.w3.org/2004/02/skos/core#>
prefix time: <http://www.w3.org/2006/time#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>

# La que hice con Mateo. 4.09 segundos
# resultado 5,120129
#SELECT ?promedioDolarPorM2
#WHERE {
#  {
#    SELECT (SUM(?valorDolares) AS ?sumaTotalDolares) (SUM(?valorEnMetros) AS ?sumaTotalM2) 
#    WHERE { 
#      # listings de terrenos en mar del plata
#      ?listing a pronto:RealEstateListing .
#      ?listing sioc:about ?terreno .
#      ?terreno a :Terreno .
#      ?terreno :hasFeature ?address .
#      ?address :hasValue ?blankNode .
#      ?blankNode a :PostalAddress .
#      ?blankNode :city ?ciudad .
#      ?ciudad rdfs:label ?nombreCiudad .
#      FILTER(CONTAINS(LCASE(?nombreCiudad), "mar del plata")) .
#
#      # valor en dólares
#      ?listing :hasFeature ?precio .
#      ?precio :hasValue ?blankNodePrecio .
#      ?blankNodePrecio a gr:UnitPriceSpecification .
#      ?blankNodePrecio gr:hasCurrency "USD"^^xsd:string  .
#      ?blankNodePrecio gr:hasCurrencyValue ?valorDolares .
#
#      # superficie en m2
#      ?terreno rec:includes ?lote .
#      ?lote :hasFeature ?superficie .
#      ?superficie :hasValue ?blankNodeSuperficie .
#      ?blankNodeSuperficie a pronto:SizeSpecification .
#      ?blankNodeSuperficie gr:hasUnitOfMeasurement "m²"^^xsd:string .
#      ?blankNodeSuperficie gr:hasValue ?valorEnMetros .
#    }
#  }
#  BIND((?sumaTotalDolares / ?sumaTotalM2) AS ?promedioDolarPorM2)
#}

# ---------------------
# Mi optimización
# Tiempo = 366 ms
# resultado = 216,08122
SELECT (AVG(?valorIndividualDolarPorMetro) AS ?promedioEnDolaresMetro2)
WHERE {
  {
    SELECT ?valorEnDolares ?valorEnMetros
    WHERE { 
      # obtenemos los terrenos
      
      ?terreno a :Terreno .
      ?terreno :hasFeature/:hasValue/:city/rdfs:label ?nombreCiudad .
      FILTER(CONTAINS(LCASE(?nombreCiudad), "mar del plata")) .

      # Obtenemos los listings de los mismos
      ?terreno ^sioc:about ?listing .

      # valor en dólares
      ?listing :hasFeature/:hasValue ?blankNodePrecio .
      ?blankNodePrecio a gr:UnitPriceSpecification .
      ?blankNodePrecio gr:hasCurrency ?monedaTerreno  .
      ?blankNodePrecio gr:hasCurrencyValue ?valorTerreno .

      BIND(
        IF(?monedaTerreno = "ARS"^^xsd:string, ?valorTerreno / 1260, ?valorTerreno)
        AS ?valorEnDolares
      )

      # superficie en m2
      ?terreno rec:includes/:hasFeature/:hasValue ?blankNodeSuperficie .
      ?blankNodeSuperficie gr:hasUnitOfMeasurement ?metricaSuperficie .
      ?blankNodeSuperficie gr:hasValue ?superficieTerreno .
      FILTER ( DATATYPE(?superficieTerreno) != xsd:string )

      BIND(
        IF(?metricaSuperficie = "ha"^^xsd:string, ?superficieTerreno * 10000, ?superficieTerreno)
        AS ?valorEnMetros
      )
    }
  }
  BIND(
    (?valorEnDolares / ?valorEnMetros) AS ?valorIndividualDolarPorMetro
  )
}